plugins {
    id 'fabric-loom' version '0.8-SNAPSHOT'
    id 'maven-publish'
    id 'java'
    id "com.github.johnrengelman.shadow" version "6.0.0"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
    maven {
        name "Modmuss50 Repository"
        url 'https://maven.fabricmc.net/'
    }
    maven { url 'https://jitpack.io' }
    maven { url 'https://www.cursemaven.com' }
    maven {
        name = "meteor-maven"
        url = "https://maven.meteordev.org/snapshots"
    }
}

shadowJar {
    dependencies {
        include(dependency("org.python:jython-standalone:2.7.2"))
    }
    exclude("**/*.exe") //because curseforge
    zip64 true
}

dependencies {
    // Minecraft
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_version}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Meteor
    modImplementation "meteordevelopment:meteor-client:SNAPSHOT"

    // Python
    implementation "org.python:jython-standalone:2.7.2"
}

build.finalizedBy(shadowJar)

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version

        filter { line -> line.replace("@mc_version@", project.minecraft_version) }
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 16
}
